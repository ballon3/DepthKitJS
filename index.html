<!DOCTYPE html>
<!-- 
	DepthKit JS Demonstration :: 2015 @obviousjim http://specular.cc
	Based on Aaronetrope by Mr Doob: http://www.aaronkoblin.com/Aaronetrope/ 
-->
<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
		<title>DepthKit JS</title>
		<link rel="stylesheet" type="text/css" href="style.css">
	</head>
	<body style="">
		<script id="vs" type="x-shader/x-vertex">

			vec3 rgb2hsl( vec3 color ) {
				float h = 0.0;
				float s = 0.0;
				float l = 0.0;
				float r = color.r;
				float g = color.g;
				float b = color.b;
				float cMin = min( r, min( g, b ) );
				float cMax = max( r, max( g, b ) );
				l =  ( cMax + cMin ) / 2.0;
				if ( cMax > cMin ) {
					float cDelta = cMax - cMin;
					// saturation
					if ( l < 0.5 ) {
						s = cDelta / ( cMax + cMin );
					} else {
						s = cDelta / ( 2.0 - ( cMax + cMin ) );
					}

					// hue
					if ( r == cMax ) {
						h = ( g - b ) / cDelta;
					} else if ( g == cMax ) {
						h = 2.0 + ( b - r ) / cDelta;
					} else {
						h = 4.0 + ( r - g ) / cDelta;
					}

					if ( h < 0.0) {
						h += 6.0;
					}
					h = h / 6.0;

				}
				return vec3( h, s, l );
			}

			// camera intrinsics from metadata file
			uniform float mindepth;
			uniform float maxdepth;
			uniform float imageWidth;
			uniform float imageHeight;
			uniform float focalX;
			uniform float focalY;
			uniform float principleX;
			uniform float principleY;

			uniform sampler2D map;
			varying float visibility;
			varying vec2 vUv;

			// projection formula
			vec3 xyz( float x, float y, float depth ) {
				float z = depth * ( maxdepth - mindepth ) + mindepth;
				return vec3( (x - principleX)  * z / focalX, 
							 (y - principleY) * z / focalY,
							 -z );
			}

			void main() {

				vUv = vec2 (position.x/imageWidth, position.y/imageHeight);
				vUv.y = vUv.y * 0.5;// + 0.5;

				vec3 hsl = rgb2hsl( texture2D( map, vUv ).xyz );
				vec4 pos = vec4( xyz( position.x, position.y, hsl.x ), 1.0 );
				pos.z += 800.0;

				visibility = hsl.z * 2.0;

				gl_PointSize = 2.0;

				gl_Position = projectionMatrix * modelViewMatrix * pos;

			}

		</script>

		<script id="fs" type="x-shader/x-fragment">

			uniform sampler2D map;
			uniform float opacity;

			varying float visibility;
			varying vec2 vUv;

			void main() {

				if ( visibility < 0.75 ) discard;

				vec4 color = texture2D( map, vUv + vec2(0.0, 0.5) );
				color.w = opacity;

				gl_FragColor = color;

			}

		</script>

		<script src="js/three.min.js"></script>
		<script src="js/tween.min.js"></script>
		<script src="js/stats.min.js"></script>
		<script src="js/system.min.js"></script>
		<script src="js/files.js"></script>
		<script src="js/RGBDVideo.js"></script>
		<script src="js/Main.js"></script>

		<canvas width="1744" height="1177" style="width: 1744px; height: 1229px;"></canvas>

	</body>
</html>